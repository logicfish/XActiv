/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar com.logicfishsoftware.example.xactive.domainmodel.Domainmodel with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate domainmodel "http://logicfishsoftware.com/xactive/example/Domainmodel"

DomainModel:
	importSection=XImportSection?
	elements+=AbstractElement*;
	
AbstractElement:
	PackageDeclaration | Entity;

PackageDeclaration:
	'package' name=QualifiedName '{' 
		elements+=AbstractElement*
	'}';

Entity: 
	annotations+=XAnnotation* 
	'entity' 
	name=ValidID (=> 'extends' superTypes+=JvmParameterizedTypeReference (=> "," superTypes+=JvmParameterizedTypeReference)*)? 
	("<" typeParameters+=JvmTypeParameter (=> "," typeParameters+=JvmTypeParameter)* ">")?
	'{' features+=Feature* '}';

Feature:
	Property | Operation;

Property: 
	annotations+=XAnnotation*  
	name=ValidID ':' type=JvmTypeReference
	;

Operation: 
	annotations+=XAnnotation* 
	'op' 
	(=> "<" typeParameters+=JvmTypeParameter (=> "," typeParameters+=JvmTypeParameter)* ">")?
	name=ValidID
	'(' (params+=FullJvmFormalParameter (=> ',' params+=FullJvmFormalParameter)*)? ')' 
	(=> ':' type=JvmTypeReference)? 
	body=XBlockExpression
	;
